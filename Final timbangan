#include <HX711_ADC.h>
#include <LiquidCrystal_I2C.h>
#include <Wire.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include <EEPROM.h>

// WiFi Configuration
const char* ssid = "scalewireless"; // nama jaringannya ini ges
const char* password = "scalejaya";

// Server Configuration
const char* serverURL = "https://e-growth.site/api/weight"; // <- API dari web ges di ubah nyesuaiin API dari website mu, pake PUT yaa ini

// HX711 pins
const int HX711_dout = 4; //DT
const int HX711_sck = 5; //SCK
HX711_ADC LoadCell(HX711_dout, HX711_sck);

// LCD
LiquidCrystal_I2C lcd(0x27, 16, 2);

// EEPROM
const int calVal_eepromAdress = 0;

// WiFi Status
bool wifiConnected = false;
unsigned long lastWiFiCheck = 0;
const unsigned long wifiCheckInterval = 30000;

// Data Sending
float lastSentWeight = -1;
unsigned long lastDataSent = 0;
const unsigned long dataSendInterval = 5000;
const float weightThreshold = 0.1;

// Buffer for Filtering
const int BUFFER_SIZE = 10;
float weightBuffer[BUFFER_SIZE];
int bufferIndex = 0;
bool bufferFull = false;
float lastStableWeight = 0;
float lastStableStdDev = 0;

unsigned long t = 0;

struct SignalInfo {
  float processedWeight;
  float displayStdDev;
  bool isStable;
  String status;
};

float roundTo005(float value) {
  return round(value / 0.05) * 0.05;
}

float movingAverage(float newValue) {
  weightBuffer[bufferIndex] = newValue;
  bufferIndex = (bufferIndex + 1) % BUFFER_SIZE;
  if (bufferIndex == 0) bufferFull = true;

  float sum = 0;
  int count = bufferFull ? BUFFER_SIZE : bufferIndex;
  for (int i = 0; i < count; i++) sum += weightBuffer[i];
  return sum / count;
}

float movingStandardDeviation(float average) {
  if (!bufferFull && bufferIndex < 2) return 0;
  float sumSquaredDiff = 0;
  int count = bufferFull ? BUFFER_SIZE : bufferIndex;
  for (int i = 0; i < count; i++) {
    float diff = weightBuffer[i] - average;
    sumSquaredDiff += diff * diff;
  }
  return sqrt(sumSquaredDiff / (count - 1));
}

SignalInfo signalProcessing(float rawWeight) {
  SignalInfo info;
  float avgWeight = movingAverage(rawWeight);
  float stdDev = movingStandardDeviation(avgWeight);
  bool isStable = (stdDev < 0.02);
  float finalWeight;
  float displayStdDev;

  if (isStable) {
    finalWeight = roundTo005(avgWeight);
    lastStableWeight = finalWeight;
    lastStableStdDev = stdDev;
    displayStdDev = stdDev;
    info.status = "STABLE";
  } else {
    finalWeight = lastStableWeight;
    displayStdDev = lastStableStdDev;
    info.status = "STABILIZING";
  }

  info.processedWeight = finalWeight;
  info.displayStdDev = displayStdDev;
  info.isStable = isStable;
  return info;
}

void connectToWiFi() {
  WiFi.begin(ssid, password);
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Connecting WiFi");

  int attempts = 0;
  while (WiFi.status() != WL_CONNECTED && attempts < 20) {
    delay(500);
    lcd.setCursor(attempts % 16, 1);
    lcd.print(".");
    attempts++;
  }

  if (WiFi.status() == WL_CONNECTED) {
    wifiConnected = true;
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("WiFi Connected");
    lcd.setCursor(0, 1);
    lcd.print(WiFi.localIP());
    delay(2000);
  } else {
    wifiConnected = false;
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("WiFi Failed");
    lcd.setCursor(0, 1);
    lcd.print("Check Settings");
    delay(2000);
  }
}

void checkWiFiStatus() {
  if (millis() - lastWiFiCheck > wifiCheckInterval) {
    if (WiFi.status() != WL_CONNECTED) {
      wifiConnected = false;
      connectToWiFi();
    } else {
      wifiConnected = true;
    }
    lastWiFiCheck = millis();
  }
}

bool sendWeightData(float weight) {
  if (!wifiConnected) return false;

  HTTPClient http;
  http.begin(serverURL);  // Use full URL

  http.addHeader("Content-Type", "application/json");

  StaticJsonDocument<64> doc;
  doc["weight"] = weight;

  String jsonString;
  serializeJson(doc, jsonString);
  Serial.println("PUT: " + jsonString);

  int httpResponseCode = http.sendRequest("PUT", jsonString);

  if (httpResponseCode > 0) {
    String response = http.getString();
    Serial.println("Response Code: " + String(httpResponseCode));
    Serial.println("Response: " + response);

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Data Sent!");
    lcd.setCursor(0, 1);
    lcd.print("Code: " + String(httpResponseCode));
    delay(1500);

    http.end();
    return (httpResponseCode >= 200 && httpResponseCode < 300);
  } else {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Send Failed");
    lcd.setCursor(0, 1);
    lcd.print("Code: " + String(httpResponseCode));
    delay(1500);

    http.end();
    return false;
  }
}

void checkAndSendWeight(float weight, bool isStable) {
  unsigned long currentTime = millis();
  if (isStable && weight > 0.5 && wifiConnected) {
    bool shouldSend = false;
    if (abs(weight - lastSentWeight) > weightThreshold) {
      shouldSend = true;
    } else if (currentTime - lastDataSent > dataSendInterval && weight > 1.0) {
      shouldSend = true;
    }

    if (shouldSend) {
      if (sendWeightData(weight)) {
        lastSentWeight = weight;
        lastDataSent = currentTime;
        Serial.println("Sent: " + String(weight, 2) + " kg");
      }
    }
  }
}

void setup() {
  Serial.begin(115200);
  EEPROM.begin(512);

  for (int i = 0; i < BUFFER_SIZE; i++) weightBuffer[i] = 0;

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Smart Scale v1.0");
  lcd.setCursor(0, 1);
  lcd.print("Initializing...");
  delay(2000);

  connectToWiFi();

  float calibrationValue;
  EEPROM.get(calVal_eepromAdress, calibrationValue);

  LoadCell.begin();
  LoadCell.start(2000, true);

  if (LoadCell.getTareTimeoutFlag()) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("HX711 Error!");
    lcd.setCursor(0, 1);
    lcd.print("Check Wiring");
    while (1);
  }

  LoadCell.setCalFactor(calibrationValue);

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Ready to Weigh");
  lcd.setCursor(0, 1);
  lcd.print(wifiConnected ? "WiFi: OK" : "WiFi: X");

  while (!LoadCell.update());
}

void loop() {
  static bool newDataReady = 0;
  const int serialPrintInterval = 250;

  checkWiFiStatus();

  if (LoadCell.update()) newDataReady = true;

  if (newDataReady && millis() > t + serialPrintInterval) {
    float rawWeight = LoadCell.getData();
    if (rawWeight < 0) rawWeight = 0;

    SignalInfo processed = signalProcessing(rawWeight);
    checkAndSendWeight(processed.processedWeight, processed.isStable);

    Serial.print("Raw: ");
    Serial.print(rawWeight, 2);
    Serial.print(" | Final: ");
    Serial.print(processed.processedWeight, 2);
    Serial.print(" | ");
    Serial.println(processed.status);

    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("W:");
    lcd.print(processed.processedWeight, 2);
    lcd.print("kg ");
    lcd.print(processed.isStable ? "S" : "U");

    lcd.setCursor(0, 1);
    lcd.print("WiFi:");
    lcd.print(wifiConnected ? "OK" : "X");

    newDataReady = 0;
    t = millis();
  }

  if (Serial.available() > 0) {
    char inByte = Serial.read();
    if (inByte == 't') {
      LoadCell.tareNoDelay();
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Taring...");
      for (int i = 0; i < BUFFER_SIZE; i++) weightBuffer[i] = 0;
      bufferIndex = 0;
      bufferFull = false;
      lastStableWeight = 0;
      lastStableStdDev = 0;
      lastSentWeight = -1;
    }
    else if (inByte == 's') {
      sendWeightData(75.5);  // manual test
    }
  }

  if (LoadCell.getTareStatus() == true) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Tare Complete");
    delay(1000);
  }
}
